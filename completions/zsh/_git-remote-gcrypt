#compdef git-remote-gcrypt
# Zsh completion for git-remote-gcrypt
# Install to: ~/.zsh/completions/ or /usr/share/zsh/site-functions/

_git_remote_gcrypt() {
	local -a args
	args=(
		'(- *)'{-h,--help}'[show help message]'
		'(- *)'{-v,--version}'[show version information]'
		'--check[check if URL is a gcrypt repository]:URL:_files'
		'1:command:(capabilities check clean fetch list push)'
		'*::subcommand arguments:->args'
	)
	_arguments -s -S $args

	case $words[1] in
	clean)
		_arguments \
			'(-f --force)'{-f,--force}'[actually delete files]' \
			'*:gcrypt URL: _alternative "remotes:gcrypt remote:($(git remote -v 2>/dev/null | grep "gcrypt::" | awk "{print \$1}" | sort -u))" "files:file:_files"'
		;;
	check)
		_arguments \
			'1:gcrypt URL:_files'
		;;
	*)
		_arguments \
			'*:gcrypt URL:'
		;;
	esac
}

_git_remote_gcrypt "$@"
